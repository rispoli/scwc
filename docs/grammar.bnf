<atom> ::= [a-zA-Z]+[a-zA-Z0-9_]*

<atom-with-multiplicity> ::= <atom>
                          |  [0-9]+ '*' <atom>

<atoms-multiset> ::= <atoms-multiset-with-multiplicity> <atom-with-multiplicity>
                  |  _empty_multiset_

<comment> ::= //.*\n

<monitor> ::= %m: <title> : <patterns-multiset>

<monitors-list> ::= <monitors-list> <monitor> %%
                 |  <monitor> %%

<openterm> ::= <atom-with-multiplicity> | (<atoms-multiset> <wvar-multiset> '|' <openterm-multiset> <tvar-multiset>)

<openterm-multiset> ::= <openterm-multiset> <openterm>
                     |  _empty_multiset_

<pattern> ::= <simple-term> | (<atoms-multiset> <wrap-variable> '|' <patterns-multiset> <term-variable>)

<patterns-multiset> ::= <patterns-multiset> <pattern>
                     |  <pattern>

<rate-function> ::= '['[0-9]+.[0-9]*']'

<rule> ::= <patterns-multiset> <term-variable> >>> <rate-function> >>> <openterm-multiset> <tvar-multiset>

<rules-list> ::= <rules-list> <rule> %%
              |  <rule> %%

<simple-term> ::= <atom-with-multiplicity>
               |  (<atoms-multiset> '|' <simple-term-multiset>)

<simple-term-multiset> ::= <simple-term-multiset> <simple-term>
                        |  _empty_multiset_

<term-variable> ::= ~[A-Z][a-zA-Z0-9_]*

<title> ::= "[^\"\n]*"

<tvar-multiset> ::= <tvar-multiset> <term-variable>
                 |  _empty_multiset_

<wrap-variable> ::= ~[a-z][a-zA-Z0-9_]*

<wvar-multiset> ::= <wvar-multiset> <wrap-variable>
                 |  _empty_multiset_

<main> ::= %MODEL: <title> %rules <rules-list> %term <simple-term-multiset> %monitors <monitors-list> %end
